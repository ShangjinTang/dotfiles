######################################################################
# EXPORTS
#=====================================================================
# customized exports, can be overrided in ~/.*shrc.local file
# proxy
export PROXY_IP="127.0.0.1:1080"
export PROXY_ENABLED=1

# security setting
export SKIP_SSL=1

# terminal theme: light | dark
export TERMINAL_THEME='dark'
case ${TERMINAL_THEME} in
    light)
        export TERMINAL_TRANSPARENT=false
        ;;
    dark)
        export TERMINAL_TRANSPARENT=true
        ;;
esac

# vim extensions
export VIM_CODEFMT_ENABLE=1

# python3_host_prog for python3 provider
export PYTHON3_HOST_PROG="/usr/bin/python3"

#=====================================================================
# default exports which do not suggest to modify

export EDITOR=nvim
export LANGUAGE=en_US.UTF-8
export LC_ALL=en_US.UTF-8
export LC_CTYPE=en_US.UTF-8
export LANG=en_US.UTF-8
export HISTFILESIZE=10000  # max size in history file, e.g. ~/.bash_history
export HISTSIZE=1000  # max size in command `history`
export HISTCONTROL=ignoreboth  # same as ignorespace:ignoredups

# Terminal Color
# xterm-256 color chart:
#   https://upload.wikimedia.org/wikipedia/commons/1/15/Xterm_256color_chart.svg
export TERM=xterm-256color
# True Color for TMUX
export COLORTERM=truecolor

# zsh
export ZSH_DISABLE_COMPFIX=1
export skip_global_compinit=1

# session type to distinguish local or ssh(remote), for zsh theme
export SESSION_TYPE=local
if [ -n "$SSH_CLIENT" ] || [ -n "$SSH_TTY" ]; then
    export SESSION_TYPE="ssh"
else
    case $(ps -o comm= -p "$PPID") in
        sshd|*/sshd) export SESSION_TYPE="ssh";;
    esac
fi

#=====================================================================
# exports for specific command line tool

# cmake
if command -v cmake &> /dev/null; then
    export CMAKE_EXPORT_COMPILE_COMMANDS=1
fi

# https://github.com/rupa/z
if command -v z &> /dev/null; then
    export _Z_NO_RESOLVE_SYMLINKS=1
fi

if command -v nnn &> /dev/null; then
    export NNN_FIFO="/tmp/nnn.fifo" # temporary buffer for the previews
    # nnn plugins reference: https://github.com/jarun/nnn/tree/master/plugins
    export NNN_PLUG='c:!code $nnn*;d:diffs;t:fzopen;p:preview-tui;z:autojump'
    nn()
    {
        if [ -z "$TMUX" ]; then
            nnn -e "$@"
        else
            nnn -e -P p "$@"
        fi
    }
    n() # to cd on quit
    {
        if [ -n $NNNLVL ] && [ "${NNNLVL:-0}" -ge 1 ]; then
            echo "nnn is already running"
            return
        fi
        export NNN_TMPFILE="${XDG_CONFIG_HOME:-$HOME/.config}/nnn/.lastd"
        if [ -z "$TMUX" ]; then
            nnn -e "$@"
        else
            nnn -e -P p "$@"
        fi
        if [ -f "$NNN_TMPFILE" ]; then
            . "$NNN_TMPFILE"
            rm -f "$NNN_TMPFILE" > /dev/null
        fi
    }
fi

######################################################################
# FUNCTIONS

#=====================================================================
# functions for local use

_DIRCREATE() {
    for arg in "$@"; do
        if [ ! -d "$arg" ]; then
            mkdir -p "$arg"
        fi
    done
}

_PATHPREPEND() {
    for arg in "$@"; do
        if [ -d "$arg" ] && [[ ":$PATH:" != *":$arg:"* ]]; then
            PATH="$arg${PATH:+":$PATH"}"
        fi
    done
}

_PATHAPPEND() {
    for arg in "$@"; do
        if [ -d "$arg" ] && [[ ":$PATH:" != *":$arg:"* ]]; then
            PATH="${PATH:+"$PATH:"}$arg"
        fi
    done
}

_PATHAPPEND ~/bin
_PATHAPPEND ~/.local/bin

# For go binaries, such as bazel 'buildifier'
_PATHAPPEND ~/go/bin
# For cargo binaries, such as lua format tool 'stylua'
_PATHAPPEND ~/.cargo/bin

# Creating npm local directory for nvim lsp
# Reference: https://docs.npmjs.com/resolving-eacces-permissions-errors-when-installing-packages-globally#manually-change-npms-default-directory
_DIRCREATE ~/.npm-global/bin && _PATHAPPEND ~/.npm-global/bin && export NPM_CONFIG_PREFIX=~/.npm-global

# Creating nvim persistent undo directory
_DIRCREATE ~/.cache/nvim/undo

#=====================================================================
# fuctions for command line use

# cdup : same as 'cd ..'
# cdup <N> (N=1~9): same as 'cd ../../..' (repeat N times)
# cdup <name>: find the nearest directory and cd into it (not support glob)
function cdup() {
    if [ $# -eq 0 ]; then
        cd ..
    elif [ $# -eq 1 ]; then
        if [[ $1 =~ '^[1-9]$' ]]; then
            local targetdir=""
            for i in {1..$1}; do
                targetdir+="../"
            done
            cd $targetdir
        elif [ $1 = '/' ]; then
            cd /
        else
            parentdir=${${PWD}%/*}/
            if [[ $parentdir == */$1/* ]]; then
                cd ${PWD%/$1/*}/$1
            fi
        fi
    fi
}
alias cu="cdup"

# cd to current/parent dirs which contains input filename
function cdup_to_exists() {
    if [ $# -eq 0 ]; then
        echo "fatal: please specify a pattern or directory, such as .root or .git"
    elif [ $# -eq 1 ]; then
        dir=${PWD}
        while [[ ! -e $dir/$1 ]] && [[ $dir != '/' ]]; do
            dir="$(dirname "$dir")"
        done
        if [[ ! -e $dir/$1 ]]; then
            echo "fatal: file $1 not found in current and parent dirs"
        else
            cd $dir
        fi
    fi
}
alias ce="cdup_to_exists"

# use fzf to scan 'wd' directories
if command -v _fzf_wd &> /dev/null; then
    function fzf_wd() {
        cd "$(_fzf_wd "$*")"
    }
fi

# Theme settings
function set_theme() {
    unalias bat &> /dev/null
    unalias ncdu &> /dev/null
    unset NNN_FCOLORS
    case $1 in
        light)
            export TERMINAL_THEME="light"
            # bat: use `bat --list-thmes` to check available colors
            export BAT_THEME="OneHalfLight"
            export NCDU_THEME="dark"
            # zsh-autosuggestions: https://github.com/zsh-users/zsh-autosuggestions#suggestion-highlight-style
            export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=252"  # default color: 8
            command -v r2 &> /dev/null && echo "eco default" > ~/.radare2rc
            ;;
        dark)
            export TERMINAL_THEME="dark"
            export BAT_THEME="OneHalfDark"
            export NCDU_THEME="dark-bg"
            BLK="04" CHR="04" DIR="04" EXE="00" REG="00" HARDLINK="00" SYMLINK="06" MISSING="00" ORPHAN="01" FIFO="0F" SOCK="0F" OTHER="02"
            export NNN_FCOLORS="$BLK$CHR$DIR$EXE$REG$HARDLINK$SYMLINK$MISSING$ORPHAN$FIFO$SOCK$OTHER"
            export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=242"
            command -v r2 &> /dev/null && echo "eco onedark" > ~/.radare2rc
            ;;
        *)
            ;;
    esac
    alias bat="bat --theme=${BAT_THEME}"
    alias ncdu="ncdu --color ${NCDU_THEME}"
    ln -sf ~/.dotfiles/configs/tmux/.tmux.conf.theme.${TERMINAL_THEME} ~/.tmux.conf.theme
}
alias setlight="set_theme light"
alias setdark="set_theme dark"

######################################################################
# ALIASES

# grep
alias grep='grep --color=auto'

# clear: do not delete scrollback buffer (requires: tput version >= 6.2)
if tput -x clear &> /dev/null; then
    alias clear='tput -x clear'
fi

# rg
alias rg='rg --smart-case'

# nvim -> v
alias v='nvim'
alias vv='nvim ~/.config/nvim/{*.lua,*.vim,lua/*.lua}'
alias vk='nvim ~/.config/nvim/lua/{plug-which-key.lua,LunarVim/lua/lvim/core/which-key.lua}'
alias vz="nvim ~/.zshrc{.pre,,.local,.post}"

# asynctask -> a
alias asynctask='~/.local/share/nvim/site/pack/lazy/opt/asynctasks.vim/bin/asynctask'
alias a=asynctask
alias af='~/.local/share/nvim/site/pack/lazy/opt/asynctasks.vim/bin/asynctask -f'
function va() {
    if [[ -f .tasks ]]; then
        nvim -O2 .tasks ~/.config/nvim/tasks.ini
    else
        # if '.tasks' not exists in current folder, open it as second buffer
        nvim ~/.config/nvim/tasks.ini .tasks
    fi
}

# exa -> e
alias exa='exa --long --group-directories-first --time-style=long-iso --git --classify --modified'
alias e='exa'
alias ea='exa --all'
alias esort='exa --sort=modified'
alias et='exa --tree'
alias e1='exa -1'
alias e2='exa --tree --level=2'
alias e3='exa --tree --level=3'
alias e4='exa --tree --level=4'
alias e5='exa --tree --level=5'

# git
alias g='git'
if command -v lazygit &> /dev/null; then
    alias gg='lazygit'
fi
alias gpristine='git reset --hard && git clean -dfx -e "cscope*" -e "tags"'
alias cg='cd $(git rev-parse --show-cdup)'

# tmux
if command -v tmux &> /dev/null; then
    function t() {
        # set default $session to $1 or 0 (if $1 not exist)
        local session="${1:-0}"
        if command -v tmuxinator &> /dev/null && [ -f ~/.tmuxinator/${session}.yml ]; then
            tmuxinator $session
        else
            if tmux has -t $session &> /dev/null; then
                if [ -z "$TMUX" ]; then
                    tmux attach -t $session
                else
                    tmux switch -t $session
                fi
            else
                if [ -z "$TMUX" ]; then
                    tmux new -s $session
                else
                    tmux new -s $session -d && tmux switch -t $session
                fi
            fi

        fi
    }
    alias tl='tmux list-sessions'
    # kill all sessions except session 0
    alias tk='tmux kill-session -at 0 &> /dev/null || tmux kill-server'
    # kill session 0
    alias tk0='tmux kill-session -t 0 &> /dev/null'
    # kill all sessions
    alias tka='tmux kill-server'
fi

# tmuxinator
if command -v tmuxinator &> /dev/null; then
    alias mux=tmuxinator
fi

# Use rg (ripgrep) as fzf's **<TAB> / CTRL-T backend
if command -v rg &> /dev/null; then
    export FZF_DEFAULT_COMMAND="rg --files --no-ignore --hidden --follow --glob '!{.git,.repo,.cache,.vscode,.build,.clangd,build,bazel-*}/*' --glob '!{.cs_files,.project,.root,.workspace,compile_commands.json,cscope.*}' 2> /dev/null"
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
fi

# fzf
# Reference: https://man.archlinux.org/man/community/fzf/fzf.1.en
if command -v fzf &> /dev/null; then
    export FZF_DEFAULT_OPTS="--bind left:preview-page-up,right:preview-page-down,ctrl-/:toggle-preview"
fi

######################################################################




######################################################################

